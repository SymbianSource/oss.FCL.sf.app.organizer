/*
* Copyright (c) 2009 Nokia Corporation and/or its subsidiary(-ies).
* All rights reserved.
* This component and the accompanying materials are made available
* under the terms of "Eclipse Public License v1.0"
* which accompanies this distribution, and is available
* at the URL "http://www.eclipse.org/legal/epl-v10.html".
*
* Initial Contributors:
* Nokia Corporation - initial contribution.
*
* Contributors:
*
* Description: Project file for Calendar Thai plugin 
*
*/

#include <platform_paths.hrh>
#include "../inc/CalenThaiPluginUids.hrh"

#include <data_caging_paths.hrh>

//Activate this macro to get traces
//MACRO           TRACE_ENTRY_EXIT_POINT
//MACRO           TRACE_MODULE_CALENTHAIPLUGIN

// Target
TARGET calenthaiplugin.dll 
TARGETTYPE PLUGIN

UID SYMBIAN_ECOM_PLUGIN_UID CALENTHAIPLUGIN_DLL_UID


// Includes 
USERINCLUDE ../inc
SYSTEMINCLUDE ../../../inc

APP_LAYER_SYSTEMINCLUDE

SYSTEMINCLUDE /epoc32/include/ecom

// Resources
SOURCEPATH ../data
// Ecom Resource 
RESOURCE CalenThaiPlugin.rss
LANGUAGE_IDS

SOURCEPATH ../src 
SOURCE CalenThaiPluginProxy.cpp
SOURCE CalenThaiPlugin.cpp


// Libraries

LIBRARY         avkon.lib
LIBRARY         calinterimapi.lib
LIBRARY         cone.lib
LIBRARY         ecom.lib
LIBRARY         eikcdlg.lib
LIBRARY         eikctl.lib
LIBRARY         eikcoctl.lib
LIBRARY         eikcore.lib
LIBRARY         euser.lib
LIBRARY         gdi.lib
LIBRARY         bafl.lib
LIBRARY         platformenv.lib
LIBRARY         efsrv.lib
LIBRARY         commonengine.lib
LIBRARY         ccon.lib 
LIBRARY 				estor.lib
LIBRARY 				centralrepository.lib
LIBRARY         cdlengine.lib

// Icons
LIBRARY         aknskins.lib
LIBRARY         aknicon.lib
LIBRARY         egul.lib

// Misc 
CAPABILITY CAP_ECOM_PLUGIN
VENDORID VID_DEFAULT
